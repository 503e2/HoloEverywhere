apply plugin: 'groovy'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.android.tools.build:gradle:0.9.+'
    compile 'org.yaml:snakeyaml:1.13'
    compile 'org.apache.maven:maven-ant-tasks:2.1.3'
    compile 'commons-io:commons-io:2.4'
}

apply plugin: 'maven'
apply plugin: 'signing'

task generateJavadoc(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

task javadoc(type: Jar, dependsOn: generateJavadoc, overwrite: true) {
    from generateJavadoc.destinationDir
    classifier = 'javadoc'
}

task sources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

boolean officialBuild = rootProject.hasProperty('officialBuild')

if (officialBuild) {
    artifacts {
        archives tasks.sources
        archives tasks.javadoc
    }
}

afterEvaluate { project ->
    signing {
        required officialBuild
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                if (officialBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                        if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                            authentication(userName: sonatypeUsername, password: sonatypePassword)
                        }
                    }
                } else {
                    repository(url: new File(rootProject.rootDir, 'repo').toURI().toString())
                }

                pom.project {
                    groupId = 'org.holoeverywhere'
                    artifactId = 'plugin'
                    name 'HoloEverywhere Plugin'
                    packaging 'jar'
                    description 'HoloEverywhere Plugin for Gradle which allow easy usage of library'
                    url 'https://github.com/Prototik/HoloEverywhere'
                    scm {
                        url 'https://github.com/Prototik/HoloEverywhere'
                        connection 'scm:git@github.com:Prototik/HoloEverywhere.git'
                        developerConnection 'scm:git@github.com:Prototik/HoloEverywhere.git'
                    }
                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://raw.github.com/Prototik/HoloEverywhere/master/LICENSE'
                            distribution 'repo'
                            comments 'Copyright (c) 2013 Sergey Shatunov'
                        }
                    }
                    developers {
                        developer {
                            id 'Prototik'
                            name 'Sergey Shatunov'
                            email 'prototypegamez@gmail.com'
                            timezone '+8'
                            roles {
                                role "developer"
                            }
                        }
                    }
                }
            }
        }
    }
}
